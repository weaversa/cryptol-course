module docs::labs::LoremIpsum::KLI20 where

import docs::labs::CRC::CRCAnswers
import docs::labs::KeyWrapping::KeyWrappingAnswers
import docs::labs::Salsa20::Salsa20Answers

/**
 * A Key Lorem-Ipsum '20 ECU emulator.
 *
 * Input: A 128-bit KEK, a 384-bit wrapped LIK (Lorem Ipsum Key), and
 * an l-byte ciphertext.
 *
 * Output: An l-byte plaintext.
 */

KLI20 : {l} (fin l, l <= 2^^70) => [128] -> [384] -> Bytes l -> Bytes l
KLI20 KEK LIK ct =
    if ~CRC1Correct then error "Outer CRC check failed"
     | ~TagCorrect  then error "Tag is incorrect"
     | ~CRC2Correct then error "Inner CRC check failed"
     | FAIL         then error "KWAD128 failed"
    else pt
  where
    TaggedKey         = take`{352} LIK
    CRC1Correct       = CRC32_C (split TaggedKey) == drop LIK
    (Tag#Issue#CRCWK) = TaggedKey
    TagCorrect        = Tag == (join "LoremIpsumKey")
    WK                = take CRCWK : [192]
    CRC2Correct       = CRC32_C (split WK) == drop CRCWK
    (FAIL, k)         = TestKWAD KEK WK
    pt                = Salsa20Encrypt (split k) (zero#(split`{3} Issue)) ct
