/*
 * Description: This is a Cryptol specification for the TKWAE and
 * TKWAD functions in [NIST-800-38F].
 */

module NIST_800_38F::TKW where

import NIST_800_38F::W32

// [NIST-800-38F] Section 4.3 Variables

ICV3 = 0xA6A6A6A6 : [32]


// [NIST-800-38F] Section 7.2 TKW

/**
 * TKW-AE (Algorithm 9) specifies the authenticated-encryption
 * function for TKW for a given block cipher and KEK. The wrapping
 * function, W, is invoked in Step 3 with the same block cipher and
 * KEK as prerequisites.
 */

TKWAE :
    {n}
    (fin n, 2 <= n, n < 2^^28 - 1) =>
    ([64] -> [64]) -> [n * 32] -> [(n + 1) * 32]
TKWAE CIPHk P = C
  where
    S = ICV3 # P
    C = W`{n+1} CIPHk S

/**
 * TKW-AD (Algorithm 10) specifies the authenticated-decryption
 * function for TKW for a given block cipher and KEK. The unwrapping
 * function, W', is invoked in Step 4 with the same block cipher and
 * KEK as prerequisites. This function returns a tuple where the first
 * element is `True` in the case of failure and the second element is
 * the unwrapped plaintext.
 */

TKWAD :
    {n}
    (fin n, 2 <= n, n < 2^^28 - 1) =>
    ([64] -> [64]) -> [(n + 1) * 32] -> (Bit, [n * 32])
TKWAD CIPHk' C = (FAIL, P)
  where
    S    = W`{n+1} CIPHk' C
    FAIL = MSB S != ICV3
    P    = LSB S

/*
===== References =====

[NIST-800-38F] (U) Recommendation for Block Cipher Modes of Operation:
Methods for Key Wrapping. Morris Dworkin. NIST Special Publication
800-38F. December 2012. This document is classified UNCLASSIFIED.

======================
*/
