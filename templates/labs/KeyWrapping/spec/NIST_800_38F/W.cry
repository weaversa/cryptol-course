/*
 * Description: This is a Cryptol specification for the W, W', and
 * other miscellaneous building blocks in [NIST-800-38F].
 */

module NIST_800_38F::W where

parameter

    type semiblock : #
    type constraint (fin semiblock, semiblock % 8 == 0)


// [NIST-800-38F] Section 4.4 Operations and Functions

/**
 * The integer for which the bit string X is the binary
 * representation.
 */

int X = toInteger X

/**
 * The bit length of bit string X.
 */

len X = length X

/**
 * The bit string consisting of the s right-most bits of the bit
 * string X.
 */
LSB : {s, a} (fin s, fin a, a >= s) => [a] -> [s]
LSB X = drop X

/**
 * The bit string consisting of the s left-most bits of the bit
 * string X.
 */
MSB : {s, a} (fin s, a >= s) => [a] -> [s]
MSB X = take X


// [NIST-800-38F] Section 6.1 W and W'

/**
 * This method specifies the wrapping function, W (Algorithm 1), for KW
 * and KWP, using the same KEK and designated cipher function.
 */

W :
    {n}
    (fin n, 3 <= n) =>
    ([2 * semiblock] -> [2 * semiblock]) -> [n * semiblock] -> [n * semiblock]
W CIPHk S = join C
  where
    type s = 6 * (n - 1)
    S'     = split`{n} S
    C      = foldl (WStep2 CIPHk) S' [ 1 .. s ]

/**
 * This method specifies the unwrapping function, W' (Algorithm 2),
 * for KW and KWP, with a given block cipher and KEK.
 */

W' :
    {n}
    (fin n, 3 <= n) =>
    ([2 * semiblock] -> [2 * semiblock]) -> [n * semiblock] -> [n * semiblock]
W' CIPHk' C = join S
  where
    type s = 6 * (n - 1)
    C'     = split`{n} C
    S      = foldl (WStep2' CIPHk') C' [ s, s - 1 .. 1 ]


private

  // [NIST-800-38F] Section 4.5 Examples of Basic Operations and
  // Functions on Strings

  property hexadecimalProp =
      0xA659 == 0b1010011001011001

  property zeroBitsProp =
      (0 : [8]) == 0b00000000

  property concatenationProp =
      0b001 # 0b10111 == 0b00110111

  property XORProp =
      0b10011 ^ 0b10101 == 0b00110

  property lenProp =
      len 0b00010 == (5 : Integer)

  property LSBProp =
      LSB`{3} 0b111011010 == 0b010

  property MSBProp =
      MSB`{4} 0b111011010 == 0b1110

  property bitstringProp =
      fromInteger 39 == 0b00100111

  property intProp =
      int 0b00011010 == 26


  // [NIST-800-38F] Section 6.1 W and W'

  /**
   * This method performs a single iteration of Step 2 of W (Algorithm
   * 1).
   */

  WStep2 :
      {n}
      (fin n, n >= 3) =>
      ([2 * semiblock] -> [2 * semiblock]) -> [n][semiblock] -> Integer -> [n][semiblock]
  WStep2 CIPHk ([A, R2] # Rs) t = [A'] # Rs # [Rn]
    where
      A' = MSB (CIPHk (A # R2)) ^ (fromInteger t)
      Rn = LSB (CIPHk (A # R2))

  /**
   * This method performs a single iteration of Step 2 of W' (Algorithm
   * 2).
   */

  WStep2' :
      {n}
      (fin n, n >= 3) =>
      ([2 * semiblock] -> [2 * semiblock]) -> [n][semiblock] -> Integer -> [n][semiblock]
  WStep2' CIPHk' ([A'] # Rs # [Rn]) t = [A, R2] # Rs
    where
      A  = MSB (CIPHk' ((A' ^ (fromInteger t)) # Rn))
      R2 = LSB (CIPHk' ((A' ^ (fromInteger t)) # Rn))

/*
===== References =====

[NIST-800-38F] (U) Recommendation for Block Cipher Modes of Operation:
Methods for Key Wrapping. Morris Dworkin. NIST Special Publication
800-38F. December 2012. This document is classified UNCLASSIFIED.

======================
*/
